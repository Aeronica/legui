plugins {
    id 'java-library'
    id "org.sonarqube" version "2.7"
    id 'net.nemerosa.versioning' version '2.6.1'
}

group 'org.liquidengine'

import groovy.json.JsonSlurper

// SETUP VERSION
def versionJson = new JsonSlurper().parseText(new File("$project.projectDir/version.json").text)
project.version = versionJson.version

printProjectInfo()

private void printProjectInfo() {
    println "##################################################"
    println "# Project info:                                  #"
    println sprintf("#     group:    %-33s#", project.group)
    println sprintf("#     name:     %-33s#", project.name)
    println sprintf("#     version:  %-33s#", project.version)
    println "##################################################"
}


configurations.all {
    resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'

idea { module { downloadJavadoc = true; downloadSources = true } }
sourceCompatibility = 11
targetCompatibility = 11
compileJava.options.encoding = 'UTF-8'

java {
    modularity.inferModulePath = true
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://raw.githubusercontent.com/SpinyOwl/repo/releases" }
}

dependencies {
    // https://mvnrepository.com/artifact/org.reflections/reflections
    api group: 'org.reflections', name: 'reflections', version: '0.9.11'

    //@formatter:off
    api group: "org.joml", name: "joml", version: joml_version, changing: true
    api group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j_version, changing: false
    api group: 'commons-io', name: 'commons-io', version: commons_io
    api group: 'org.apache.commons', name: 'commons-lang3', version: commons_version, changing: false
    api group: 'org.apache.commons', name: 'commons-collections4', version: commons_collections, changing: false
    api group: 'com.google.guava', name: 'guava', version: guava_version, changing: false
    api group: 'com.google.code.gson', name: 'gson', version: gson_version, changing: false

    api group: 'org.liquidengine', name: 'cbchain', version: le_cbchain, changing: true

    api group: 'org.lwjgl', name: 'lwjgl-stb', version: lwjgl_version, changing: true
    api group: 'org.lwjgl', name: 'lwjgl-stb', version: lwjgl_version, changing: true, classifier: 'natives-windows'
    api group: 'org.lwjgl', name: 'lwjgl-stb', version: lwjgl_version, changing: true, classifier: 'natives-linux'
    api group: 'org.lwjgl', name: 'lwjgl-stb', version: lwjgl_version, changing: true, classifier: 'natives-macos'

    api group: 'org.lwjgl', name: 'lwjgl-glfw', version: lwjgl_version, changing: true
    api group: 'org.lwjgl', name: 'lwjgl-glfw', version: lwjgl_version, changing: true, classifier: 'natives-windows'
    api group: 'org.lwjgl', name: 'lwjgl-glfw', version: lwjgl_version, changing: true, classifier: 'natives-linux'
    api group: 'org.lwjgl', name: 'lwjgl-glfw', version: lwjgl_version, changing: true, classifier: 'natives-macos'

    api group: 'org.lwjgl', name: 'lwjgl-nanovg', version: lwjgl_version, changing: true
    api group: 'org.lwjgl', name: 'lwjgl-nanovg', version: lwjgl_version, changing: true, classifier: 'natives-windows'
    api group: 'org.lwjgl', name: 'lwjgl-nanovg', version: lwjgl_version, changing: true, classifier: 'natives-linux'
    api group: 'org.lwjgl', name: 'lwjgl-nanovg', version: lwjgl_version, changing: true, classifier: 'natives-macos'

    api group: 'org.lwjgl', name: 'lwjgl-opengl', version: lwjgl_version, changing: true
    api group: 'org.lwjgl', name: 'lwjgl-opengl', version: lwjgl_version, changing: true, classifier: 'natives-windows'
    api group: 'org.lwjgl', name: 'lwjgl-opengl', version: lwjgl_version, changing: true, classifier: 'natives-linux'
    api group: 'org.lwjgl', name: 'lwjgl-opengl', version: lwjgl_version, changing: true, classifier: 'natives-macos'

    api group: 'org.lwjgl', name: 'lwjgl', version: lwjgl_version, changing: true
    api group: 'org.lwjgl', name: 'lwjgl', version: lwjgl_version, changing: true, classifier: 'natives-windows'
    api group: 'org.lwjgl', name: 'lwjgl', version: lwjgl_version, changing: true, classifier: 'natives-linux'
    api group: 'org.lwjgl', name: 'lwjgl', version: lwjgl_version, changing: true, classifier: 'natives-macos'

    api group: 'org.lwjgl', name: 'lwjgl-yoga', version: lwjgl_version, changing: true
    api group: 'org.lwjgl', name: 'lwjgl-yoga', version: lwjgl_version, changing: true, classifier: 'natives-windows'
    api group: 'org.lwjgl', name: 'lwjgl-yoga', version: lwjgl_version, changing: true, classifier: 'natives-linux'
    api group: 'org.lwjgl', name: 'lwjgl-yoga', version: lwjgl_version, changing: true, classifier: 'natives-macos'
    //@formatter:on

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

test {
    useJUnitPlatform()
}

// PUBLISH SECTION
configurations {
    deployerJar
}
repositories {
    maven { url 'https://raw.githubusercontent.com/synergian/wagon-git/releases' }
}
dependencies {
    deployerJar 'ar.com.synergian:wagon-git:0.3.0'
}

def publishUrl = System.getProperty("publishUrl") ?: System.getenv("publishUrl") ?: null
def publishUsername = System.getProperty("publishUsername") ?: System.getenv("publishUsername") ?: null
def publishPassword = System.getProperty("publishPassword") ?: System.getenv("publishPassword") ?: null

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJar
        repository(url: publishUrl) {
            authentication(userName: publishUsername, password: publishPassword)
        }
    }
}

//@formatter:off
jar {
    exclude("org/liquidengine/legui/demo/**")
    manifest {
        attributes(
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision' : versioning.info.commit,
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Full-Version'   : project.version
        )
    }
}
//@formatter:on

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from(sourceSets.main.allSource) {
        exclude("org/liquidengine/legui/demo/**")
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from(javadoc.destinationDir) {
        exclude("org/liquidengine/legui/demo/**")
    }
}

javadocJar.dependsOn javadoc
uploadArchives.dependsOn jar
uploadArchives.dependsOn sourcesJar
uploadArchives.dependsOn javadocJar

artifacts {
    archives sourcesJar
    archives javadocJar
}


// SONARQUBE CONFIGURATION


def sonarLogin = System.getProperty("sonarLogin") ?: System.getenv("sonarLogin") ?: null
sonarqube {
    properties {
        property "sonar.projectKey", "SpinyOwl_legui"
        property "sonar.organization", "spinyowl"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", sonarLogin
    }
}